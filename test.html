<!DOCTYPE html>
<html>
<head>
    <title>VBAコード表示</title>
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/styles/default.min.css">
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/highlight.min.js"></script>
    <script>hljs.initHighlightingOnLoad();</script>
</head>
<body>
    <pre><code class="vb">
Option Compare Database

Public Const BASEMAIL_PATH As String = "E:\work\" ' 保存フォルダパス


Sub SaveSpecificEmails()
    Dim i As Integer
    Dim olApp As Outlook.Application
    Dim olNS As Outlook.NameSpace
    Dim olInbox As Outlook.MAPIFolder
    Dim olItems As Outlook.Items
    Dim olItem As Object
    Dim olMailItem As Outlook.MailItem
    Dim olAttach As Outlook.Attachment
    Dim fso As Object
    Dim safeSubject As String

    Const SEARCH_EMAILS As Integer = 100 ' 処理件数
    Const titleFilter As String = "質問" ' 検索するキーワード

    ' Outlookアプリケーションを取得
    Set olApp = CreateObject("Outlook.Application")
    Set olNS = olApp.GetNamespace("MAPI")
    Set olInbox = olNS.GetDefaultFolder(olFolderInbox)

    ' Restrictを使用して、部分一致する件名のメールを絞り込む
    Set olItems = olInbox.Items.Restrict("@SQL=""urn:schemas:httpmail:subject"" LIKE '%" & titleFilter & "%'")
    olItems.Sort "[ReceivedTime]", True ' 受信日時で降順にソート

    ' FileSystemObject を作成
    Set fso = CreateObject("Scripting.FileSystemObject")

    ' BASE_PATH の存在確認
    If Not fso.FolderExists(BASEMAIL_PATH) Then
        fso.CreateFolder BASEMAIL_PATH
    End If

    i = 0

    ' 絞り込んだメールを処理
    If olItems.Count > 0 Then
        For Each olItem In olItems
            ' 件数が最大値に達したら終了
            If i >= SEARCH_EMAILS Then Exit For

            ' メールアイテムかどうかを確認
            If olItem.Class = olMail Then
                Set olMailItem = olItem

                ' フォルダ名の生成と特殊文字の置換
                safeSubject = SanitizeFolderName(olMailItem.Subject)
                strFolderName = Format(Date, "yyyyMMdd") & "_" & safeSubject
                strPath = BASEMAIL_PATH & strFolderName

                ' フォルダを作成
                If Not fso.FolderExists(strPath) Then
                    fso.CreateFolder strPath
                End If

                ' 添付ファイルを保存
                For Each olAttach In olMailItem.Attachments
                    olAttach.SaveAsFile strPath & "\" & olAttach.FileName
                Next olAttach

                ' メールを保存
                On Error Resume Next
                olMailItem.SaveAs strPath & "\" & strFolderName & ".msg", olMSG
                On Error GoTo 0

                ' 処理件数を増加
                i = i + 1
            End If
        Next olItem

        MsgBox i & "件のメールを処理しました。" & vbCrLf & "保存先: " & BASEMAIL_PATH, vbInformation
    Else
        MsgBox "条件に一致するメールはありません。"
    End If

    ' オブジェクトの解放
    Set olAttach = Nothing
    Set olMailItem = Nothing
    Set olItem = Nothing
    Set olItems = Nothing
    Set olInbox = Nothing
    Set olNS = Nothing
    Set olApp = Nothing
    Set fso = Nothing
End Sub

Public Function SanitizeFolderName(ByVal folderName As String) As String
    Dim invalidChars As Variant
    Dim char As Variant

    invalidChars = Array("/", "\", ":", "*", "?", """", "<", ">", "|")
    For Each char In invalidChars
        folderName = Replace(folderName, char, "_")
    Next char
    SanitizeFolderName = folderName
End Function


Option Compare Database


Sub OpenFolder(ByVal folderPath As String)
    Dim shellApp As Object

    ' パスをトリムして余分な文字を削除
    folderPath = Trim(folderPath)
    folderPath = Replace(folderPath, vbCrLf, "")
    folderPath = Replace(folderPath, vbTab, "")
    folderPath = CStr(folderPath)
    If Right(folderPath, 1) = "\" Then
        folderPath = Left(folderPath, Len(folderPath) - 1)
    End If

    ' フォルダの存在確認
    If Dir(folderPath, vbDirectory) = "" Then
        MsgBox "指定されたフォルダが存在しません: " & folderPath, vbExclamation
        Exit Sub
    End If

    ' フォルダを開く
    On Error Resume Next
    Set shellApp = CreateObject("Shell.Application")
    shellApp.Open folderPath
    If Err.Number <> 0 Then
        MsgBox "フォルダを開く際にエラーが発生しました: " & Err.Description, vbExclamation
    End If
    On Error GoTo 0

    ' オブジェクト解放
    Set shellApp = Nothing
End Sub


            </code></pre>
</body>
</html>
